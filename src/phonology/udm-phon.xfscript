
!  This file is a script for the Xerox xfst interface.

clear stack
echo << THE DEFININTIONS >>

echo << defining Vow >>

define Vow [ а | е | ё | и | ӥ | о | ӧ | ы | э | ю | я 
           | А | Е | Ё | И | Ӥ | О | Ӧ | Ы | Э | Ю | Я 
! like the cyrillic...               ö   bl  eab ju  ja
           ];

define PalVow [ е | ё | и | ю | я 
              | Е | Ё | И | Ю | Я 
              ];

echo << defining Cns >>
define Cns [ б | в | г | д | ж | ӝ | з | ӟ 
           | Б | В | Г | Д | Ж | Ӝ | З | Ӟ 
! like the cyr..             zh  zh"  z   z"
           | й | к | л | м | н | п | р | с | т | ф | х | ц 
           | Й | К | Л | М | Н | П | Р | С | Т | Ф | Х | Ц 
           | ч | ӵ | ш | щ | ъ | ь 
           | Ч | Ӵ | Ш | Щ | Ъ | Ь 
! like cyr   ch  ch"  sh  shch hrd sft
           ];

define NonPalatalCons [ б | в | г | д | ж | ӝ  
                      | Б | В | Г | Д | Ж | Ӝ  
                          | к     | м     | п | р         | ф | х     
                          | К     | М     | П | Р         | Ф | Х     
                              | ш | щ | ъ     
                              | Ш | Щ | Ъ     
                      ];

define Syll [ (Cns) (Cns) Vow (Cns) (Cns) ] ; 

echo << defining Boundary >>
define Boundary %> ;





! Consonant alternations in certain pos
! -------------------------------------



! Vowel shifts
! ------------

echo << defining the rules >>

define ijaDeletion [я -> 0, е -> 0 || Syll Syll _ Boundary ];

define PalatalisedSuffixes э -> е, o -> ё  || Vow Boundary ъ _ ;

! Either shift to the palatal series after an actual palatal sound, or the
! consonants that cannot be palatized

define PalatalVowelShift э -> e, о -> ё, ӥ -> и || [ NonPalatalCons | ь | и ] Boundary _ ;
define PalatalYaShift    а -> я || [ NonPalatalCons | ь | и ] Boundary _ ;

define PalatalIRemoval ӥ -> и || [ Vow | NonPalatalCons ] Boundary _ ;

! TODO: for some reason not working here: возьe
define PalatalMarkDeletion ь -> 0 || _ Boundary [ PalVow | э ] ;

define HardMarkDeletion ъ -> 0 || [ ь | Vow] Boundary _ ;

define Syncope ӧ2 (->) 0, е2 (->) 0, о2 (->) 0 || _ Cns %> (ъ) Vow ;

define ParagogicCns к2 -> к, м2 -> м, т2 -> т || _ %> Vow ;

echo << defining cleanup rules >>

define Cleanup ӧ2 -> ӧ, е2 -> е, о2 -> о ;

define BoundaryDeletion [ %> -> 0 ] ;

define Deletion к2 -> 0, м2 -> 0, т2 -> 0, %@ -> 0;
!                %^@ -> 0 ;

echo << composing different parts >>

read regex [ 
ijaDeletion
.o.
PalatalisedSuffixes
.o.
PalatalIRemoval
.o.
PalatalVowelShift
.o.
PalatalYaShift
.o.
PalatalMarkDeletion
.o.
HardMarkDeletion
.o.
Syncope
.o.
ParagogicCns
.o.
Cleanup
.o.
BoundaryDeletion
.o.
Deletion 
] ;


