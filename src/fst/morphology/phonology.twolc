!! # Udmurt twol file

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-udm/blob/main/src/fst/phonology.twolc) 

!! ## Alphabet, Sets and Definitions

Alphabet

!! ## Letters of the alphabet

а б в г д е ё ж ӝ з ӟ и ӥ й к л м н о ӧ п р с т у ф х ц ч ӵ ш щ ъ ы ь э ю я !!= * @CODE@
А Б В Г Д Е Ё Ж Ӝ З Ӟ И Ӥ Й К Л М Н О Ӧ П Р С Т У Ф Х Ц Ч Ӵ Ш Щ Ъ Ы Ь Э Ю Я !!= * @CODE@

!! ## Archiphonemes for vowels
!  е1:е а1:а и1:и у1:у ӱ1:ӱ
!  Е1:Е И1:И У1:У Ӱ1:Ӱ

%^@:0  	   !!= @CODE@ This apparently causes :о +V+Ind+PrtII:%>еме%>%^@ +V+Ind+Fut:%>о%>%^@ +V+Ind+PrtI:%>и%>%^@ 
!! ## Triggers

%^ConsTransf:0 !!= * @CODE@ 
%^KIN:0        !!= * @CODE@  with ӓвӓ in +PxSg1+Sg+Gen etc.
%^RusJa:0      !!= * @CODE@ Эстония:Эстониез
%^RusJaErr:0   !!= * @CODE@ Эстония:Эстонияез
%^NotRusJa:0   !!= * @CODE@ no words in ия ие

%^SYNC:0       !!= * @CODE@ 
%^ЫLossVar:0      !!= * @CODE@ This is used for мыныны: мыны ~ мын 
%^ЫLoss:0      !!= * @CODE@ This is used for мыныны: мын 

Ы2:0	       !!= * @CODE@ morpheme initial disappears after vowel ы 0

%^Е2:0	       !!= * @CODE@ morpheme initial disappears after vowel е э 0
%^И2:0	       !!= * @CODE@ morpheme initial disappears after vowel и ӥ 0
%^О2:о	       !!= * @CODE@ morpheme initial disappears after vowel also ло after stem-final а
%^Е3:е %^Е3:э %^Е3:ы       !!= * @CODE@ Acc, Ill е э ы
%^Ы3:и %^Ы3:ы       !!= * @CODE@ Acc, PX in some nouns
%^А3:0	       !!= * @CODE@ Ine а, я
%^Л3:0	       !!= * @CODE@ ужаны:ужало
%^К3:0	       !!= * @CODE@ нюлэс:нюлэс%^К3
%^М3:0	       !!= * @CODE@ син:син%^М3
%{еэ%}:е	       !!= * @CODE@ morpheme initial vowel е э , e.g. Acc


!! ## Boundary symbols

 %> !!= * @CODE@ 
 #  !!  * hash #
 %- !!= * @CODE@ 
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <


 ;


!! ## Sets 

Sets
BackLabVow =   о у ё ю О У Ё Ю ;
NonLabVow =   а е и ӥ э я А Е И Ӥ Э Я ;
Vow =  а е ё и ӥ о ӧ у ы э ю я 
       А Е Ё И Ӥ О Ӧ У Ы Э Ю Я ;
BackVow = BackLabVow а А ы Ы ;
FrontVow = FrontLabVow е и ӥ э Е И Ӥ Э ;
ArchiVow =  Ы2 ;
FullVow = BackLabVow FrontLabVow NonLabVow ;

CnsNoLN =  б в г д ж з к м ҥ п р с т ф х ц ч ш 
           Б В Г Д Ж З К М Ҥ П Р С Т Ф Х Ц Ч Ш ;

CnsAlv = д з л н р с т ц
         Д З Л Н Р С Т Ц ;

CnsNoPair = б в г ж ӝ ӟ к м п р ф ч ӵ ш щ
            Б В Г Ж Ӝ Ӟ К М П Р Ф Ч Ӵ Ш Щ ;

CnsPair = д з л н с т ц
          Д З Л Н С Т Ц ;

CnsVoiceless =  к п с т ф х ц ч ӵ ш щ
                К П С Т Ф Х Ц Ч Ӵ Ш Щ ;


Cns =  б в г д ж ӝ з ӟ к л м н п р с т ф х ц ч ӵ ш щ 
       Б В Г Д Ж Ӝ З Ӟ К Л М Н П Р С Т Ф Х Ц Ч Ӵ Ш Щ ;

Cst =  к п т ф х ц ч ш с 
       К П Т Ф Х Ц Ч Ш Ц ;

Ltrs = Vow Cns Й й  ъ ь Ъ Ь ;

all =  Ltrs  %- ;



!! ## Definitions

Definitions

!LFC =  # [ Cns | й ]*  [ :FrontVow | %^Е2: | :е  ] \[ #: | :BackVow | :ы | :Ы ]* (%{аӓ%}:я ) \[ #: | :BackVow | :ы | :Ы ]*  (%^А2ЫӸ:0) ;



!! ## Rules

Rules



!! ## DEPALATALISATION

"Depalatalize и"
!! **@RULENAME@**
 и:ӥ <=> CnsPair %>  _ ;

"Depalatalize е"
!! **@RULENAME@**
 %^Е2:э <=> [ CnsPair | у ] ы: %>  _ ;
!!€ мыны%>%^Е2
!!€ мын0%>э

"Depalatalized %{еэ%} "
!! **@RULENAME@**
%{еэ%}:э <=> [ CnsPair | у ] ы: %>  _ ;
!!€ укмысназ%>%{еэ%}с
!!€ укмысназ%>эс

"Depalatalize и"
!! **@RULENAME@**
 %^И2:ӥ <=> CnsPair ы: %>  _ ;
!!€ мыны%>%^И2сько
!!€ мын0%>ӥсько

"Depalatalize е3:е"
!! **@RULENAME@**
 %^Е3:е <=> [ CnsNoPair (%^SYNC:)  | [ о | и | ы | а ] (%^RusJa:|%^RusJaErr:|%^NotRusJa:) | я: (%^RusJa:|%^NotRusJa:|%^RusJaErr:) | е: [%^RusJa:|%^NotRusJa:|%^RusJaErr:] | ь: | й: ] %> _ ;
!!€ бакча%>%^Е3
!!€ бакча%>е

"Depalatalize е3:э"
!! **@RULENAME@**
 %^Е3:э <=> [ CnsPair: - [ с | С ]| ы:0 ] (%^SYNC:) %>  _ ;
          [ Ltrs: - [ ё:|Ё:]] [ с | C ] (%^SYNC:) %>  _ ;
          [ Ltrs: - [ ъ:|Ъ:]] [ ё:|Ё:] [ с | C ] (%^SYNC:) %>  _ ;
! [[ (Ltrs:) Ltrs: Ltrs: CnsPair: ] - [ %> ъ: ё: с ]| ы: ] (%^SYNC:) %>  _ ;

"Depalatalize е3:е"
!! **@RULENAME@**
 %^Ы3:и <=> [ ь: | й: ] %> _ ;


"Depalatalize Ы3:ы"
!! **@RULENAME@**
 %^Ы3:ы <=> [ Cns| Vow ] (%^SYNC:) %>  _ ;


"Depalatalize and raise е3:ы"
!! **@RULENAME@**
 %^Е3:ы <=>  %> ъ: ё: с %>  _ ;

"Depalatalize and raise ё:о"
!! **@RULENAME@**
 ё:о <=> Vow: (%^RusJa:|%^NotRusJa:|%^RusJaErr:) %> ъ: _ с ;
!!€ корка%>ъёс
!!€ корка%>0ос
!!€ собрание%^RusJa%>ъёс
!!€ собрани00%>0ос


"%^А3:а"
!! **@RULENAME@**
 %^А3:а <=> Cns %>  _ ;

"%^О2:о"
!! **@RULENAME@**
 %^О2:о <=> [ [Cns|Vow] ы: (%>) | [Vow - [ ы: | й: ]] %> ( %^Л3:0 ) ] _ ;
!!€ мыны%>%^О2
!!€ мын0%>о
!!€ ужа%>0%^О2
!!€ ужа%>ло

"Palatal %^О2:ё"
!! **@RULENAME@**
 %^О2:ё <=>  [ ь: | й: ] (ы:) (%>)  _ ;


"Palatal э:е"
!! **@RULENAME@**
 э:е <=>  [ Vow | CnsNoPair ] :0* %> _  ;

"Palatal е"
!! **@RULENAME@**
 %^Е2:е <=> [CnsNoPair|ь:|ъ:| й:] ы: %>  _ ;

"Palatal и"
!! **@RULENAME@**
 %^И2:и <=> [CnsNoPair|ь:|ъ:|у| й:] ы: %>  _ ;
!!€ луы%>%^И2з
!!€ лу0%>из

"Palatal а"
!! **@RULENAME@**
 %^А3:я <=> [ь:| й:|Vow:] %>  _ ;

"Palatal и:й"
!! **@RULENAME@**
 %^И2:й <=> [ а | я ] %>  _ [ #| %>] ;


!! ## VOWEL LOSS
"Stem vowel loss ы:0 LEFT" !! Is this really necessary
!! **@RULENAME@**
 ы:0 <= _ %> [ %^Е2: | %{еэ%}: | %^И2: | %^О2: ] ;
!!€ луы%>%^Е2м
!!€ лу0%>эм
         Vow Cns _ [ %^ЫLoss: | %^О2: ] ;

"Stem vowel loss Ы2:0 RIGHT"
!! **@RULENAME@**
 ы:0 => _  %> [ %^Е2: | %{еэ%}: | %^И2: | %^О2: ] ;
!!€ луы%>%^Е2м
!!€ лу0%>эм
         Vow :Cns (ь:) _ [ %^ЫLossVar: | %^ЫLoss: | %^О2: ] ; 
!!€ мыны#
!!€ мыны0
!!€ мын00
!!€ возьы#
!!€ возьы0
!!€ возь00

"я:0"
!! **@RULENAME@**
 я:0 <=>  _ %^RusJa: %> [%^Е3: | ъ: ё: | ы | л ] ;

"е:0"
!! **@RULENAME@**
 е:0 <=>  _ %^RusJa: %> [%^Е3: | ъ: ё: | ы | л ] ;
          [ CnsNoPair | р | Р ] _ Cns %^SYNC: %> :Vow ;

"е:ь"
!! **@RULENAME@**
 е:ь <=> CnsPair _ Cns %^SYNC: %> :Vow ;

"э:0"
!! **@RULENAME@**
 э:0 <=>  Cns _ Cns %^SYNC: %> :Vow ;


"Palatal е:0"
!! **@RULENAME@**
 %^Е2:0 <=> [ а | я ] %>  _ ;

"Palatal и:0"
!! **@RULENAME@**
 %^И2:0 <=> [ а | я ] %>  _ Cns ;

"%^О2:0"
!! **@RULENAME@**
 %^О2:0 <=> [ а | я ]  _ ;
!!€ ужа%>%^О2
!!€ ужа%>0

"й:0"
!! **@RULENAME@**
 й:0 <=> _ (%>) [ %^И2: | %^Е2: | %^О2: | %^Е3: ] ;

"ь:0"
!! **@RULENAME@**
 ь:0 <=> _ (ы:) (%>) [ %^И2: | %^Е2: | %^О2: | %^Е3: ] ;
!!€ возь%>Ы2#
!!€ возь%>ы0

"ъ:0"
!! **@RULENAME@**
 ъ:0 <=> Vow: (%^RusJa:|%^NotRusJa:|%^RusJaErr:) %> _ ;
!!€ корка%>ъёс
!!€ корка%>0ос
!!€ собрание%^RusJa%>ъёс
!!€ собрани00%>0ос
     [ ь | й ] %> _ [ я | ё ] ;

!! ## ZERO TO CONSONANT
"%^Л3:л"
!! **@RULENAME@**
 %^Л3:л <=> Vow %> _ %^О2: ;
!!€ ужа%>%^Л3%^О2
!!€ ужа%>ло

!! ## CONSONANTS

"Devoicing д:т"
!! **@RULENAME@**
 д:т <=> CnsVoiceless %> _ ;

"Devoicing з:с"
!! **@RULENAME@**
 з:с <=> CnsVoiceless %> _ ;

"Surface consonant before vowel %^К3:к"
!! **@RULENAME@**
 %^К3:к <=> _ %> :Vow ;

"Surface consonant before vowel %^М3:м"
!! **@RULENAME@**
 %^М3:м <=> _ %> :Vow ;

"Disallow %^NotRusJa after cons or other vowels"
!! **@RULENAME@**
%^NotRusJa:0 /<= и [ е: | я:]  _ ;

"Disallow %^RusJa after cons or other vowels"
!! **@RULENAME@**
%^RusJa:0 /<= [Cns: | о | а | и | ы | у | ю | ё | ъ | ь| Cns: [ е: | я:] ] _ ;

"Disallow %^RusJaErr after cons or other vowels"
!! **@RULENAME@**
%^RusJaErr:0 /<= [Cns: | о | а | и | ы | у | ю | ё | ъ | ь| Cns: [ е: | я:] ] _ ;

