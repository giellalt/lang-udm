!! !!!Udmurt Proper noun inflection

!! !! Place names based on names of old tribes

!! These words typically end on -я, –га, –ка, –ра, –ла. When inflecting in some
!! cases, they take an infix which is optional.

!! The following may be exhaustive, but unsure yet.

!! * -я: Салья, Монья, Кибъя, Чабъя, Ӟумъя, Вамъя
!! * -га: Можга, Пурга, Дурга, Нылга, Эбга, Коньга
!! * -ка: Поска
!! * -ра: Бигра, Эгра
!! * -ла: Чола

!! Infix is optional in some cases

!! || Case    ||  Infix || No infix
!! | Iness.    |  Салья-ла-н   |  Салья-ын
!! | Illat.     |  Салья-ла      |  Салья-е
!! | Elat.      |  Салья-ла-сь   |  Салья-ысь
!! | Egress.    |  Салья-ла-сен  |  Салья-ысен

!! The following cases do not have the infix at all

!! || Case    ||  No infix
!!  | Terminat.  |  Салья-озь
!!  | Prol.      |  Салья-тӥ
!!  | Appr.      |  Салья-лань


LEXICON PROP_
!! So far the PROP (underline) lexicon is a dummy pexicon that just gives __+N+Prop__,
!! and then redirects to ''NOUNSUF_01''.
+N+Prop: NOUNSUF_01 ;

LEXICON PROP_RUS_JA
+N+Prop:%^RusJa NOUNSUF_01 ;
+N+Prop+Err/Orth: NOUNSUF_01 ;

LEXICON PROP_HEIMONNIMET
!! Words from lexicon __@LEXNAME@__ are "old" tribe and place names, 
!! which have an optional infix for some cases, while some do not.
+N+Prop+Sg:%>ла HEIMO_SUFFIXES_LA ;
+N+Prop+Sg:     HEIMO_SUFFIXES_NIL ;

LEXICON HEIMO_SUFFIXES_LA
!! In __@LEXNAME@__, we append the cases with their post-ла realizations.
!! Examples:
!! || Case    ||  Infix || No infix
!! | Iness.     |  Салья-ла-н    |  Салья-ын
!! | Illat.     |  Салья-ла      |  Салья-е
!! | Elat.      |  Салья-ла-сь   |  Салья-ысь
!! | Egress.    |  Салья-ла-сен  |  Салья-ысен

!! Note also that the infix results in a 'short' case affix.
+Ine:%>н   K;
+Ill:%>0   K;
+Ela:%>сь  K;
+Egr:%>сен K;
+Acc:%>з   K;

LEXICON HEIMO_SUFFIXES_NIL
!! In __@LEXNAME@__, the word gets cases with no infix.

!!  || Case  || wordform
!!  | Terminat.  |  Салья-озь
!!  | Prol.      |  Салья-тӥ
!!  | Appr.      |  Салья-лань
! These are the obligatorily non-infixing cases
+Nom:%>0 K;
+Prl:%>етӥ K;
+Ter:%>озь K;
+Apr:%>лань K;
! These are the ones that optionally also have no infix,
! but longer case forms.
+Ill:%>е K;
+Ela:%>ысь K;
+Egr:%>ысен K;
+Ine:%>ын K;

! TODO: do these have an optional infix, or not?
! I'll assume that there's information in the book I'm reading, but I just
! haven't gotten to it yet... 
+Ins:%>ен K;
+Acc:%>ез K;
+Gen:%>лэн K;
+Abl:%>лэсь K;
+Dat:%>лы K;
+Abe:%>тэк K;
+Advl:%>я K;



