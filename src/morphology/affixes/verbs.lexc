!! !!! Udmurt verb inflection
!!
!! Udmurt verbs are mainly split into two types: 
!!  * conjugation I, whose stems mostly end on ''ы'' (ex. ''мыны-ны'' 'go', ''юы-ны'' 'drink', ''вераськы-ны'' 'speak')
!!  * conjugation II, whose stems mostly end on ''а/я'' (ex. ''ужа.ны'' 'work', ''келя.ны'' 'send, put', ''кырӟа.ны'' 'sing')

!! Conjugation I verbs also may have a 'short' stem, which subtracts the final
!! ''ы''. The rule is not necessarily as simple as suffixes that begin on
!! consonants require a vowel-ending stem, because there are some exceptions.
!! Following is a list of things that use the short stem (so far
!! incomplete, but feel free to add to it)
!!
!!  * All forms of the ''present affirmative''
!!  * All forms of the ''present negative''
!!  * All forms of the ''future affirmative''
!!  * All forms of the ''future negative''
!!  * Preterite II Negative? 
!!  * Caritive gerund? 
!!  
!! And the long stem:
!!
!!  * Infinitive
!!  * Conditional affirmative, negative
!!  * basic gerund, temporal gerund, fourth gerund ?
!!  * gerunds: instrumental -(э)мен/-(е)мен, elative -(э)мысь/-(е)мысь ?

!! There are some orthographic things to consider, and they show up in the way
!! verbs are inflected. The palatizing series [d z l n s t] is represented 
!! in two ways: vowel letters following, or by the palatizing symbol. 

!!      возьыны    ..   возе    ..   возисько
!!      бертыны    ..   бертэ   ..   бертӥсько

LEXICON V_ !!= @CODE@ Verb type is still to be determined.
+WORK+V: #;

!! Typical V_MONO entry is the long stem, ending on <ы>
!!  берты, возьы

!!  бертыны:берт V_MONO  "mennä kotiin" ;

LEXICON V_MONO
!! Lexicon ''@LEXNAME@''
!! * Yaml: __mynyny__
   V_MONO_SHORT ;
:ы V_MONO_LONG ; 

LEXICON V_MONO_LONG
+V+Ind+Fut:%>о FUTPAST ;
+V+Ind+PrtI:%>и FUTPAST ;
+V+Ind+PrtII:%>еме FUTPASTSG ;
+V+Ind+PrtII:%>ем FUTPASTPL ;
+V+Cond:%>сал COND ;
+V+Opt:%>о OPT ;
+V: NONFINITES ;

LEXICON V_MONO_SHORT
+V+Ind+Prs: OPRS ;

LEXICON V_UZA
!! Lexicon ''@LEXNAME@''
!! * Yaml: __uzhany__
+V+Ind+Prs: APRS ;
+V+Ind+Fut:%>ло FUT-PERSON ;
+V+Ind+Fut+ConNeg+Sg: K ;
+V+Ind+Fut+ConNeg+Pl:%>лэ K ;
+V+Ind+PrtI:  PRTI-PERSON ;
+V+Ind+PrtI+ConNeg+Sg: K ;
+V+Ind+PrtI+ConNeg+Pl:%>лэ K ;
+V+Ind+PrtII: PRTII-PERSON ;
+V+Cond:%>сал COND ;
+V+Imprt: IMPRT ;
+V+Opt: OPT ;
+V: NONFINITES ;

! Which variant to present should be predicable from stem, after
! /y/ deletion

!!      возь >   ..   возе    ..   возисько
!!      берт >   ..   бертэ   ..   бертӥсько

! TODO: -y stems in V_MONO somehow
! nuny lapsi - wrong tag
! TODO: part of lexc instead? 
! TODO: пуны / пу 
! cat verbs.xml| grep 'Contlex' | grep 'MONO' | grep -v 'ы<'


LEXICON OPRS
!! Lexicon ''@LEXNAME@''
+Sg1:%>ӥсько K ;
+Sg2:%>ӥськод K ;
+Sg3:%>э K ;
+Pl1:%>ӥськом K ;
+Pl1:%>ӥськомы K ;
+Pl2:%>ӥськоды K ;
+Pl3:%>о K ;


LEXICON APRS
!! Lexicon ''@LEXNAME@''
+Sg1:%>сько K ;
+Sg2:%>ськод K ;
+Sg3: K ;
+Pl1:%>ськом K ;
+Pl1:%>ськомы K ;
+Pl2:%>ськоды K ;
+Pl3:%>ло K ;
+ConNeg+Sg1:%>ськы K ;
+ConNeg+Sg2:%>ськы K ;
+ConNeg+Sg3: K ;
+ConNeg+Pl1:%>ське K ;
+ConNeg+Pl2:%>ське K ;
+ConNeg+Pl3:%>ло K ;

LEXICON FUTPAST
!! Lexicon ''@LEXNAME@''
FUTPASTSG ;
FUTPASTPL ;
FUTPASTPLSHORT ;

LEXICON FUTPASTSG 
!! Lexicon ''@LEXNAME@''
+Sg1:%>%^@ K ;
+Sg2:%>д K ;
+Sg3:%>з K ;

LEXICON FUTPASTPL 
!! Lexicon ''@LEXNAME@''
+Pl1:%>мы K ;
+Pl2:%>ды K ;
+Pl3:%>зы K ;

LEXICON FUTPASTPLSHORT 
!! Lexicon ''@LEXNAME@''
+Pl1:%>м K ;

LEXICON FUT-PERSON
!! Lexicon ''@LEXNAME@''
+Sg1: K ;
+Sg2:%>д K ;
+Sg3:%>з K ;
+Pl1:%>м K ;
+Pl2:%>ды K ;
+Pl3:%>зы K ;

LEXICON PRTI-PERSON
!! Lexicon ''@LEXNAME@''
+Sg1:%>й K ;
+Sg2:%>д K ;
+Sg3:%>з K ;
+Pl1:%>мы K ;
+Pl1:%>м K ;
+Pl2:%>ды K ;
+Pl3:%>зы K ;

LEXICON PRTII-PERSON
!! Lexicon ''@LEXNAME@''
+Sg1:%>ськем K ;
+Sg2:%>мед K ;
+Sg3:%>м K ;
+Pl1:%>ськеммы K ;
+Pl2:%>ллямды K ;
+Pl3:%>ллямзы K ;
+Pl3:%>ллям K ;
+ConNeg+Sg1:%>ськем K ;
+ConNeg+Sg2:%>мед K ;
+ConNeg+Sg3:%>м K ;
+ConNeg+Pl1:%>ськеммы K ;
+ConNeg+Pl2:%>ллямды K ;
+ConNeg+Pl3:%>ллямзы K ;
+ConNeg+Pl3:%>ллям K ;

+Neg+Sg1:%>ськымтэе K ;
+Neg+Sg2:%>мтэед K ;
+Neg+Sg3:%>мтэ K ;
+Neg+Pl1:%>ськымтэмы K ;
+Neg+Pl2:%>мтэды K ;
+Neg+Pl3:%>ллямтэзы K ;
+Neg+Pl3:%>ллямтэ K ;


LEXICON COND
!! Lexicon ''@LEXNAME@''
+Sg1:          K ;
+Sg2:          K ;
+Sg2:ыд        K ;
+Sg3:          K ;
+Sg3:ыз       K ;
+Pl1:мы        K ;
+Pl2:ды        K ;
+Pl3:зы        K ;
+ConNeg+Sg1:   K ;
+ConNeg+Sg2:   K ;
+ConNeg+Sg2:ыд K ;
+ConNeg+Sg3:   K ;
+ConNeg+Sg3:ыз K ;
+ConNeg+Pl1:мы K ;
+ConNeg+Pl2:ды K ;
+ConNeg+Pl3:зы K ;

LEXICON IMPRT
!! Lexicon ''@LEXNAME@''
+Sg2: K	   ;
+Sg3:%>лоз K	;
+Pl2:%>лэ K	;
+Pl3:%>лозы K	;
+ConNeg+Sg2: K	;
+ConNeg+Sg3: K	;
+ConNeg+Pl2:%>лэ K	;
+ConNeg+Pl3:%>лэ K	;

LEXICON OPT
!! Lexicon ''@LEXNAME@''
+Sg1:%>м K ;
+Sg2:%>д K ;
+Sg3:%>з K ;
+Pl1:%>мы K	;
+Pl2:%>ды K	;
+Pl3:%>зы K	;
+ConNeg+Sg: K ;
+ConNeg+Pl: K ;
+ConNeg+Pl:%>лэ K	;

LEXICON NONFINITES
    !! Lexicon ''@LEXNAME@''
+Inf:%>ны  K	;
+GerPos:%>са  K	;
+GerNeg:%>тэк  K	;
+PrcPrsNeg:%>сьтэм K ;
+PrcPrsPos:%>сь K ;
+PrcPrfNeg:%>мтэ K ;
+PrcPrfAdn:%>м K ;
+PrcPrfPred:%>мын K ;

! vim: set ft=xfst-lexc:

